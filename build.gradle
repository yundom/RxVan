buildscript {
    ext.kotlin_version = '1.2.31'
    ext.junit_version = '4.12'
    ext.version_name = '0.0.1'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.1"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'com.yundom'
version version_name

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            groupId 'com.yundom'
            artifactId 'rxvan'
            version version_name
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER_NAME") ?: ''
    key = System.getenv("BINTRAY_API_KEY") ?: ''
    publications = ['BintrayPublication']
    pkg {
        repo = 'RxVan'
        name = 'RxVan'
        desc = 'A lightweight event bus written in Kotlin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/yundom/rxvan'
        version {
            name = "v$version_name"
            released = new Date()
            vcsTag = version_name
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.reactivex.rxjava2:rxjava:2.1.14"

    testCompile "junit:junit:$junit_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}